System.setProperty("file.encoding", "UTF-8")

apply plugin: 'java'
apply plugin: 'distribution'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
		classpath("com.aragost.javahg:javahg:+")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
        classpath("org.springframework.build.gradle:propdeps-plugin:+")
		classpath("org.ajoberstar:gradle-git:+")
    }
}

distTar.enabled = false
distZip.dependsOn(":loginserver:distZip")
distZip.dependsOn(":gameserver:distZip")

distributions {
	main {
		baseName = 'UNGP'
		contents {
			from 'loginserver/build/distributions'
			from 'gameserver/build/distributions'
		}
	}
}

subprojects {
    apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'org.ajoberstar.grgit'

    sourceCompatibility = '1.8'

	repositories {
		mavenCentral()
	}

	eclipse {
		project {
			natures('org.springsource.ide.eclipse.gradle.core.nature')
		}
	}

	tasks.eclipse.doLast {
		copy {
			from('../eclipse-settings')
			into('.settings')
		}
	}

	tasks.cleanEclipse.doLast {
		delete('.settings')
	}
	
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

    tasks.withType(Jar) {
        manifest {
            attributes(
					'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
					'Built-By': System.getProperty('user.name'),
					'Implementation-Time': new Date().format('yyyy-MM-dd_hh:mm'),
					'Implementation-Version': grgit.log(includes:['HEAD']).size(),
					'Git-Branch': grgit.branch.getCurrent().getName(),
					'Git-Hash-Full': grgit.head().id,
					'Git-Hash-Short': grgit.head().getAbbreviatedId()
            )
        }
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}